!
! THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
! IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
! THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
! (AT YOUR OPTION) ANY LATER VERSION.
!
! THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL,
! BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
! MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  SEE THE
! GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
!
! YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
! ALONG WITH THIS PROGRAM; IF NOT, SEE <HTTP://WWW.GNU.ORG/LICENSES/>.
!
! COPYRIGHT (C) Martin Koehler, 2017-2022
!

PROGRAM MAIN
    USE CSC_COMMON
    USE CSC_TABLE
    USE CSC_INIFILE
    IMPLICIT NONE


    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:) :: A, B, C, D, X, Y, YBAK
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:) :: WORK

    DOUBLE PRECISION, DIMENSION(20) :: MACHINE

    INTEGER :: M, MSTART, MSTOP, MSTEP, RUNS, R, MAT, NMAT
    INTEGER, DIMENSION(4) :: ISEED
    INTEGER :: SORT
    CHARACTER(1) :: TRANSA, TRANSB
    DOUBLE PRECISION, PARAMETER :: DONE = 1.0D0
    DOUBLE PRECISION :: SCAL
    INTEGER :: INFO, LWORK
    INTEGER, PARAMETER :: NB = 64, MB = 64
    DOUBLE PRECISION :: TS, TE

    CHARACTER(LEN=1024) :: OUTPUT
    EXTERNAL MEPACK_INITIALIZE
    EXTERNAL BENCHMARK_INIT_F
    EXTERNAL BENCHMARK_RANDOM_GEVP_DOUBLE_F
    EXTERNAL BENCHMARK_RHS_GSYLV_DOUBLE_F
    EXTERNAL BENCHMARK_CHECK_X_DOUBLE_F
    EXTERNAL DLA_TGSYLV_L3_ROWWISE, DLA_TGSYLV_L3_COLWISE, DLA_TGSYLV_L3
    EXTERNAL DGEMM
    DOUBLE PRECISION BENCHMARK_CHECK_X_DOUBLE_F

    ! Table
    TYPE(C_PTR) :: TAB
    INTEGER :: COLM, COLCWISE, COLRWISE, COLRESCWISE, COLRESRWISE, COLOPT1, COLRESOPT1

    ! TIMES
    DOUBLE PRECISION :: TCOLWISE, TROWISE, TOPT
    DOUBLE PRECISION :: RCOLWISE, RROWISE, ROPT

    CALL MEPACK_INITIALIZE()

    TAB = CSC_TABLE_NEW(1)
    COLM           = CSC_TABLE_ADD_COLUMN(TAB, "M", CSC_TABLE_INTEGER, CSC_TABLE_RIGHT)
    COLRWISE       = CSC_TABLE_ADD_COLUMN(TAB, "Time Row.Wise (L3_ROWISE)", CSC_TABLE_FLOAT, CSC_TABLE_RIGHT)
    COLRESRWISE    = CSC_TABLE_ADD_COLUMN(TAB, "Res. Row.Wise ", CSC_TABLE_FLOAT, CSC_TABLE_RIGHT)
    COLCWISE       = CSC_TABLE_ADD_COLUMN(TAB, "Time Col.Wise (L3_COLWISE)", CSC_TABLE_FLOAT, CSC_TABLE_RIGHT)
    COLRESCWISE    = CSC_TABLE_ADD_COLUMN(TAB, "Res. Col.Wise", CSC_TABLE_FLOAT, CSC_TABLE_RIGHT)
    COLOPT1        = CSC_TABLE_ADD_COLUMN(TAB, "Time Col-Opt (L3)", CSC_TABLE_FLOAT, CSC_TABLE_RIGHT)
    COLRESOPT1     = CSC_TABLE_ADD_COLUMN(TAB, "Res. Col-Opt", CSC_TABLE_FLOAT, CSC_TABLE_RIGHT)
    CALL CSC_TABLE_COMMENT_ALLINFO(TAB)

    MSTART = 1
    MSTOP  = 2100
    ! MSTART = 2035
    ! MSTOP  = 2060
    MSTEP  = 1
    RUNS   = 10
    NMAT = 5
    MAT = 0

    ISEED = 1
    SORT = 0

    TRANSA = 'N'
    TRANSB = 'N'


    WRITE(*,'("# ", A)') "Benchmark the row- and column-wise computation of the solution for "
    WRITE(*,'("# ", A)') "generalized Sylvester equation."


    CALL BENCHMARK_INIT_F()

    DO M = MSTART, MSTOP, MSTEP
        ISEED = 1
        TROWISE = 0.0D0
        TCOLWISE = 0.0D0
        TOPT =0.0D0
        RROWISE = 0.0D0
        RCOLWISE = 0.0D0
        ROPT =0.0D0


        CALL CSC_TABLE_NEW_ROW(TAB)

        DO MAT = 1, NMAT
            ALLOCATE(A(M,M), B(M,M), C(M,M), D(M,M), X(M,M), Y(M,M), YBAK(M, M) )
            X = DONE
            LWORK =  MAX(2*MAX(NB,MB),MAX(MB*NB, M*NB))
            ALLOCATE(WORK(LWORK))

            MACHINE(6) = 5
            MACHINE(4) = MB
            MACHINE(5) = NB

            CALL BENCHMARK_RANDOM_GEVP_DOUBLE_F(M, ISEED, A, C, SORT)
            CALL BENCHMARK_RANDOM_GEVP_DOUBLE_F(M, ISEED, D, B, SORT)
            CALL DGEMM("N", "N", M, M, M, DONE, A, M, C, M, DONE, Y, M)

            CALL BENCHMARK_RHS_GSYLV_DOUBLE_F(TRANSA, TRANSB, DONE, M, M, A, M, B, M, C, M, D, M, X, M, Y, M)
            YBAK = Y

            ! Row Wise
            TE = 0.0D0
            DO R = 1, RUNS
                Y = YBAK
                TS = CSC_WTIME()
                CALL DLA_TGSYLV_L3_ROWWISE(TRANSA, TRANSB, DONE, M, M, A, M, B, M, C, M, D, M, Y, M, SCAL, WORK, INFO, MACHINE)
                TE = TE + (CSC_WTIME() - TS)
            END DO
            TROWISE = TROWISE + (TE / DBLE (RUNS))
            RROWISE = RROWISE + BENCHMARK_CHECK_X_DOUBLE_F(M, M, Y, M, X, M)

            ! Colwise
            TE = 0.0D0
            DO R = 1, RUNS
                Y = YBAK
                TS = CSC_WTIME()
                CALL DLA_TGSYLV_L3_COLWISE(TRANSA, TRANSB, DONE, M, M, A, M, B, M, C, M, D, M, Y, M, SCAL, WORK, INFO, MACHINE)
                TE = TE + (CSC_WTIME() - TS)
            END DO
            TCOLWISE = TCOLWISE + TE / DBLE (RUNS)
            RCOLWISE = RCOLWISE + BENCHMARK_CHECK_X_DOUBLE_F(M, M, Y, M, X, M)

            ! Opt
            TE = 0.0D0
            DO R = 1, RUNS
                Y = YBAK
                TS = CSC_WTIME()
                CALL DLA_TGSYLV_L3(TRANSA, TRANSB, DONE, M, M, A, M, B, M, C, M, D, M, Y, M, SCAL, WORK, INFO, MACHINE)
                TE = TE + (CSC_WTIME() - TS)
            END DO
            TOPT = TOPT + TE / DBLE (RUNS)
            ROPT = ROPT + BENCHMARK_CHECK_X_DOUBLE_F(M, M, Y, M, X, M)

            DEALLOCATE(A, B, C, D, X, Y, YBAK)
            DEALLOCATE(WORK)

        END DO

        CALL CSC_TABLE_SET_ENTRY_INTEGER(TAB, COLM, M)
        CALL CSC_TABLE_SET_ENTRY_FLOAT(TAB, COLRWISE, TROWISE/DBLE(NMAT))
        CALL CSC_TABLE_SET_ENTRY_FLOAT(TAB, COLRESRWISE, RROWISE/DBLE(NMAT))
        CALL CSC_TABLE_SET_ENTRY_FLOAT(TAB, COLCWISE, TCOLWISE/DBLE(NMAT))
        CALL CSC_TABLE_SET_ENTRY_FLOAT(TAB, COLRESCWISE, RCOLWISE/DBLE(NMAT))
        CALL CSC_TABLE_SET_ENTRY_FLOAT(TAB, COLOPT1, TOPT/DBLE(NMAT))
        CALL CSC_TABLE_SET_ENTRY_FLOAT(TAB, COLRESOPT1, ROPT/DBLE(NMAT))
    END DO

    CALL CSC_TABLE_COMMENT_CMD(TAB)
    CALL CSC_TABLE_COMMENT_DATE(TAB)
    CALL CSC_TABLE_COMMENT_TEXT(TAB, "MSTART:  " // INT2STR(MSTART))
    CALL CSC_TABLE_COMMENT_TEXT(TAB, "MSTOP:   " // INT2STR(MSTOP))
    CALL CSC_TABLE_COMMENT_TEXT(TAB, "MSTEP:   " // INT2STR(MSTEP))
    CALL CSC_TABLE_COMMENT_TEXT(TAB, "RUNS:    " // INT2STR(RUNS))
    CALL CSC_TABLE_COMMENT_TEXT(TAB, "NMAT:    " // INT2STR(NMAT))


    CALL CSC_TABLE_PRINT(TAB, "    ")
    IF ( COMMAND_ARGUMENT_COUNT() .EQ. 1) THEN
        CALL GET_COMMAND_ARGUMENT(1, OUTPUT)
        CALL CSC_TABLE_SAVE_ASCII(TRIM(OUTPUT), TAB, "   ")
    END IF

    CALL CSC_TABLE_DESTROY(TAB)


END PROGRAM MAIN



